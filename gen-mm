#!/usr/bin/perl

use strict;
use warnings;

use JSON;

my %next;

for my $c1 ('a' .. 'z', '$') {
    for my $c2 ('a' .. 'z', '$') {
        for my $c3 ('a' .. 'z', '$') {
            $next{$c1}{$c2}{$c3} = 0;
        }
    }
}

while (<>) {
    chomp;
    tr/A-Z/a-z/;
    s/[^a-z ]//g;

    my @words = split /\s/;
    for my $word (@words) {
        my @chars = split //, $word;

        my $state1 = '$';
        my $state2 = '$';
        for my $c (@chars) {
            $next{$state1}{$state2}{$c}++;
            $state1 = $state2;
            $state2 = $c;
        }
        $next{$state1}{$state2}{'$'}++;
        $next{$state2}{'$'}{'$'}++;
    }
}

for my $c (keys %next) {
    for my $c1 (keys %{ $next{$c} }) {
        my $count = 0.0;

        for my $v (keys %{ $next{$c}{$c1} }) {
            $count += $next{$c}{$c1}{$v} + 1;
        }

        for my $v (keys %{ $next{$c}{$c1} }) {
            $next{$c}{$c1}{$v} = ($next{$c}{$c1}{$v} + 1) / $count;
        }
    }
}

print "var markov=", encode_json(\%next), ";\n";
